#!zsh

if ! (( ${+functions[asdf]} )); then
    source "${ZINIT[PLUGINS_DIR]}/asdf/lib/asdf.sh"
fi

local plugin=${1:-$(asdf plugin list all | fzf | awk '{print $1}')}
if [[ -z "${plugin}" ]]; then
    return 0
fi

if [[ ! -e "${ASDF_DATA_DIR}/plugins/${plugin}" ]]; then
    asdf plugin add "${plugin}"
else
    asdf plugin update "${plugin}"
fi

local -a versions=()
case "$2" in
current)
    local -A current=($(<${ASDF_DEFAULT_TOOL_VERSIONS_FILENAME}))
    versions=(${current[plugin]:-$(asdf latest ${plugin})})
    ;;

latest)
    versions=($(asdf latest ${plugin}))
    ;;

"")
    versions=($(asdf list all ${plugin} | fzf --tac -m))
    ;;

*)
    versions=(${=2})
    ;;
esac

local version
for version (${versions}); do
    if [[ ! -e "${ASDF_DATA_DIR}"/installs/${plugin}/${version} ]]; then
        asdf install "${plugin}" "${version}"
        asdf global  "${plugin}" "${version}"
    fi
done
