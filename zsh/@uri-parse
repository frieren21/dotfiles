#!zsh

# https://stackoverflow.com/questions/6174220/parse-url-in-shell-script/45977232#45977232
#
# Following regex is based on https://tools.ietf.org/html/rfc3986#appendix-B with
# additional sub-expressions to split authority into userinfo, host and port
#
readonly URI_REGEX='^(([^:/?#]+):)?(//((([^:/?#]+)@)?([^:/?#]+)(:([0-9]+))?))?(/([^?#]*))(\?([^#]*))?(#(.*))?'
#                    ↑↑            ↑  ↑↑↑            ↑         ↑ ↑            ↑ ↑        ↑  ↑        ↑ ↑
#                    |2 scheme     |  ||6 userinfo   7 host    | 9 port       | 11 rpath |  13 query | 15 fragment
#                    1 scheme:     |  |5 userinfo@             8 :…           10 path    12 ?…       14 #…
#                                  |  4 authority
#                                  3 //…

local part="$1" uri="$2"

typeset -A _uri_parts=(
    scheme 2
    authority 4
    user 6
    host 7
    port 9
    path 10
    rpath 11
    query 13
    fragment 15
)

[[ "${uri}" =~ $URI_REGEX ]] && \
REPLY=${match[${_uri_parts[${part}]}]}
